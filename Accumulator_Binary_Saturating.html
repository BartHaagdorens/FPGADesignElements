<html>
<head>
<link rel="shortcut icon" href="./favicon.ico">
<link rel="stylesheet" type="text/css" href="./style.css">
<meta name="viewport" content="width=device-width, initial-scale=1">
<title>Accumulator Binary Saturating</title>
</head>
<body>

<p><a href="./Accumulator_Binary_Saturating.v">Source</a></p>

<h1>Binary Accumulator, Signed, with Saturation</h1>
<p>Adds the signed <code>increment</code> to the signed <code>accumulated_value</code> every cycle
 <code>increment_valid</code> is high. <code>load_valid</code> overrides <code>increment_valid</code> and
 instead loads the accumulator with <code>load_value</code>. <code>clear</code> overrides both
 <code>increment_valid</code> and <code>load_valid</code> and puts the accumulator back at
 <code>INITIAL_VALUE</code>.</p>
<p>If the increment would cause the accumulator to go past the signed minimum
 or maximum limits, the accumulator will saturate at the nearest limit value
 and also raise <code>signed_overflow</code> until the next operation.</p>
<p>When chaining accumulators, which may happen if you are incrementing in
 unusual bases where each digit has its own accumulator, AND the <code>carry_out</code>
 of the previous accumulator with the signal fed to the <code>increment_valid</code>
 input of the next accumulator. The <code>carry_in</code> is kept for generality.</p>

<pre>
`default_nettype none

module <a href="./Accumulator_Binary_Saturating.html">Accumulator_Binary_Saturating</a>
#(
    parameter                   WORD_WIDTH      = 0,
    parameter [WORD_WIDTH-1:0]  INITIAL_VALUE   = 0
)
(
    input   wire                        clock,
    input   wire                        clear,
    input   wire    [WORD_WIDTH-1:0]    max_limit,
    input   wire    [WORD_WIDTH-1:0]    min_limit,
    input   wire    [WORD_WIDTH-1:0]    increment,
    input   wire                        increment_valid,
    input   wire    [WORD_WIDTH-1:0]    load_value,
    input   wire                        load_valid,
    input   wire                        carry_in,
    output  wire                        carry_out,
    output  wire    [WORD_WIDTH-1:0]    accumulated_value,
    output  wire                        signed_overflow
);

    localparam WORD_ZERO = {WORD_WIDTH{1'b0}};
</pre>

<p>Apply the increment to the current accumulator value, with saturation.</p>

<pre>
    wire [WORD_WIDTH-1:0] incremented_value;

    <a href="./Adder_Subtractor_Binary_Saturating.html">Adder_Subtractor_Binary_Saturating</a>
    #(
        .WORD_WIDTH (WORD_WIDTH)
    )
    add_increment
    (
        .max_limit  (max_limit),
        .min_limit  (min_limit),
        .add_sub    (1'b0), // 0/1 -> A+B/A-B
        .carry_in   (carry_in),
        .A_in       (accumulated_value),
        .B_in       (increment),
        .sum_out    (incremented_value),
        .carry_out  (carry_out)
    );
</pre>

<p>Then, let's <a href="./CarryIn_Binary.html">reconstruct the carry-in</a> into the last
 (most-significant) bit position of the result. If it differs from the
 carry_out, then a signed overflow/underflow happened, where we either added
 past the largest positive value or subtracted past the smallest negative
 value (regardless of saturation).</p>

<pre>
    wire final_carry_in;

    <a href="./CarryIn_Binary.html">CarryIn_Binary</a>
    #(
        .WORD_WIDTH (1)
    )
    calc_final_carry_in
    (
        .A          (accumulated_value  [WORD_WIDTH-1]),
        .B          (increment          [WORD_WIDTH-1]),
        .sum        (incremented_value  [WORD_WIDTH-1]),
        .carryin    (final_carry_in)
    );

    reg signed_overflow_internal = 1'b0;

    always @(*) begin
        signed_overflow_internal = (carry_out != final_carry_in);
    end
</pre>

<p>Update the accumulator if load or increment is valid. 
 <em>Load overrides increment.</em> 
 Clear the overflow if loading.</p>

<pre>
    reg [WORD_WIDTH-1:0]    next_value              = WORD_ZERO;
    reg                     enable_accumulator      = 1'b0;
    reg                     enable_overflow         = 1'b0;
    reg                     clear_overflow          = 1'b0;

    always @(*) begin
        next_value          = (load_valid == 1'b1) ? load_value : incremented_value;
        enable_accumulator  = (increment_valid == 1'b1) || (load_valid == 1'b1);
        enable_overflow     = (increment_valid == 1'b1);
        clear_overflow      = (load_valid      == 1'b1) || (clear      == 1'b1);
    end
</pre>

<p>Finally, the accumulator and signed_overflow registers.</p>

<pre>
    <a href="./Register.html">Register</a>
    #(
        .WORD_WIDTH     (WORD_WIDTH),
        .RESET_VALUE    (INITIAL_VALUE)
    )
    accumulator
    (
        .clock          (clock),
        .clock_enable   (enable_accumulator),
        .clear          (clear),
        .data_in        (next_value),
        .data_out       (accumulated_value)
    );

    <a href="./Register.html">Register</a>
    #(
        .WORD_WIDTH     (1),
        .RESET_VALUE    (1'b0)
    )
    overflow
    (
        .clock          (clock),
        .clock_enable   (enable_overflow),
        .clear          (clear_overflow),
        .data_in        (signed_overflow_internal),
        .data_out       (signed_overflow)
    );

endmodule
</pre>

<hr>
<p><a href="./index.html">back to FPGA Design Elements</a>
<center><a href="http://fpgacpu.ca/">fpgacpu.ca</a></center>
</body>
</html>

