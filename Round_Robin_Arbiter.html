<html>
<head>
<link rel="shortcut icon" href="./favicon.ico">
<link rel="stylesheet" type="text/css" href="./style.css">
<meta name="viewport" content="width=device-width, initial-scale=1">
<title>Round Robin Arbiter</title>
</head>
<body>

<p><a href="./Round_Robin_Arbiter.v">Source</a></p>

<h1>A Round-Robin Arbiter</h1>
<p>Returns a one-hot grant bitmask selected from one of the raised request
 bits in a word, in round-robin order, going from least-significant to
 most-significant bit, and back around.  Unset request bits are skipped,
 which avoids wasting time. Requests can be raised or dropped before their
 turn comes, but this must be done synchronously to the clock. Grants take
 one cycle to update after the requests change.</p>
<p>Here, we implement a "mask method" round-robin arbiter, as described in
 Section 4.2.4, Figure 12 of Matt Weber's <a href="http://citeseerx.ist.psu.edu/viewdoc/download?doi=10.1.1.86.550&amp;rep=rep1&amp;type=pdf">Arbiters: Design Ideas and Coding
 Styles</a>. </p>
<p>A round-robin arbiter is commonly used to fairly divide a resource amongst
 multiple requestors, in proportion to each requestor's activity. Idle
 requestors don't use up any of the arbitrated resource. A frequent
 requestor will not obstruct other requestors perpetually.</p>
<p>However, this round-robin arbiter does not deal with some subtleties of
 fairness. For example, it's possible that under normal operation, one
 requestor ends up placing a request always just before the previous
 requestor finishes. Thus, that new request waits less than the others. One
 solution periodically takes a snapshot of the current pending requests, and
 services all of these requests before refreshing the snapshot.</p>
<p>A common use-case for an arbiter is to drive a <a href="./Multiplexer_One_Hot.html">one-hot
 multiplexer</a> to select one of multiple senders
 requesting for one receiver, or one of multiple receivers requesting from
 one sender. This arrangement requires that the requestors can raise and
 hold a "request" signal, wait until they receive the "grant" signal to
 begin their transaction, and to drop "request" only once they are done.
 This is very similar to a ready/valid handshake, except that the
 transaction cannot be interrupted, else the granted access is lost.</p>

<pre>
`default_nettype none

module <a href="./Round_Robin_Arbiter.html">Round_Robin_Arbiter</a>
#(
    parameter WORD_WIDTH = 0
)
(
    input   wire                        clock,
    input   wire                        clear,
    input   wire    [WORD_WIDTH-1:0]    requests,
    output  wire    [WORD_WIDTH-1:0]    grant
);

    localparam ZERO = {WORD_WIDTH{1'b0}};
</pre>

<p>Grant a request in priority order (LSB has higest priority)</p>

<pre>
    wire [WORD_WIDTH-1:0] grant_raw;

    <a href="./Priority_Arbiter.html">Priority_Arbiter</a>
    #(
        .WORD_WIDTH (WORD_WIDTH)
    )
    raw_grants
    (
        .requests   (requests),
        .grant      (grant_raw)
    );
</pre>

<p>Mask-off all requests of higher priority than the request granted in the
 previous cycle.</p>

<pre>
    wire [WORD_WIDTH-1:0] mask;

    <a href="./Bitmask_Thermometer_to_Rightmost_1_Bit.html">Bitmask_Thermometer_to_Rightmost_1_Bit</a>
    #(
        .WORD_WIDTH (WORD_WIDTH)
    )
    grant_mask
    (
        .word_in    (grant),
        .word_out   (mask)
    );

    reg [WORD_WIDTH-1:0] requests_masked;

    always @(*) begin
        requests_masked = requests & mask;
    end
</pre>

<p>Grant a request in priority order, but from the masked requests (equal or
 lower priority to the request granted last cycle)</p>

<pre>
    wire [WORD_WIDTH-1:0] grant_masked;

    <a href="./Priority_Arbiter.html">Priority_Arbiter</a>
    #(
        .WORD_WIDTH (WORD_WIDTH)
    )
    masked_grants
    (
        .requests   (requests_masked),
        .grant      (grant_masked)
    );
</pre>

<p>If no granted requests remain after masking, then grant from the unmasked
 requests, which starts over granting from the highest (LSB) priority. This
 also resets the mask. And the process begins again.</p>

<pre>
    reg [WORD_WIDTH-1:0] grant_next = ZERO;

    always @(*) begin
        grant_next = (grant_masked == ZERO) ? grant_raw : grant_masked; 
    end

    <a href="./Register.html">Register</a>
    #(
        .WORD_WIDTH     (WORD_WIDTH),
        .RESET_VALUE    (ZERO)
    )
    granted_requests
    (
        .clock          (clock),
        .clock_enable   (1'b1),
        .clear          (clear),
        .data_in        (grant_next),
        .data_out       (grant)
    );

endmodule
</pre>

<hr>
<p><a href="./index.html">back to FPGA Design Elements</a>
<center><a href="http://fpgacpu.ca/">fpgacpu.ca</a></center>
</body>
</html>

