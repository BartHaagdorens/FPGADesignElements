<html>
<head>
<link rel="shortcut icon" href="./favicon.ico">
<link rel="stylesheet" type="text/css" href="./style.css">
<meta name="viewport" content="width=device-width, initial-scale=1">
<title>Accumulator Binary</title>
</head>
<body>

<p><a href="./Accumulator_Binary.v">Source</a></p>

<h1>Binary Accumulator</h1>
<p>Adds the <code>increment</code> to the <code>accumulated_value</code> every cycle
 <code>increment_valid</code> is high. <code>load_valid</code> overrides <code>increment_valid</code> and
 instead loads the accumulator with <code>load_value</code>. <code>clear</code> overrides both
 <code>increment_valid</code> and <code>load_valid</code> and puts the accumulator back at
 <code>INITIAL_VALUE</code>.</p>
<p>If the <code>SATURATING</code> parameter is zero, the accumulator will wrap around if
 it goes below the smallest negative number or largest positive number the
 accumulator can hold, and will also raise <code>signed_overflow</code> until the next
 operation.</p>
<p>If <code>SATURATING</code> is non-zero, the accumulator will saturate at these limit
 values if you try to go past them, and also raise <code>signed_overflow</code> until
 the next operation.</p>
<p>Regardless of the value of <code>SATURATING</code>, if the limits are reached exactly,
 or if we load the accumulator, then <code>signed_overflow</code> is not set.</p>
<p>When chaining accumulators, which may happen if you are incrementing in
 unusual bases where each digit has its own accumulator, AND the <code>carry_out</code>
 of the previous accumulator with the signal fed to the <code>increment_valid</code>
 input of the next accumulator. The <code>carry_in</code> is kept for generality.</p>

<pre>
`default_nettype none

module Accumulator_Binary
#(
    parameter                   WORD_WIDTH      = 0,
    parameter [WORD_WIDTH-1:0]  INITIAL_VALUE   = 0,
    parameter                   SATURATING      = 0
)
(
    input   wire                        clock,
    input   wire                        clear,

    input   wire    [WORD_WIDTH-1:0]    increment,
    input   wire                        increment_valid,
    input   wire    [WORD_WIDTH-1:0]    load_value,
    input   wire                        load_valid,
    input   wire                        carry_in,
    output  wire                        carry_out,
    output  wire    [WORD_WIDTH-1:0]    accumulated_value,
    output  wire                        signed_overflow
);

    localparam WORD_ZERO = {WORD_WIDTH{1'b0}};
    localparam WORD_MIN  = {1'b1,{WORD_WIDTH-1{1'b0}}};
    localparam WORD_MAX  = {1'b0,{WORD_WIDTH-1{1'b1}}};
</pre>

<p>Apply the increment to the current accumulator value.</p>

<pre>
    wire [WORD_WIDTH-1:0] incremented_value;

    Adder_Subtractor_Binary
    #(
        .WORD_WIDTH (WORD_WIDTH)
    )
    add_increment
    (
        .add_sub    (1'b0), // 0/1 -> A+B/A-B
        .carry_in   (carry_in),
        .A_in       (accumulated_value),
        .B_in       (increment),
        .sum_out    (incremented_value),
        .carry_out  (carry_out)
    );
</pre>

<p>Then, let's <a href="./CarryIn_Binary.html">reconstruct the carry-in</a> into the last
 (most-significant) bit position of the result. If it differs from the
 carry_out, then a signed overflow/underflow happened, where we either added
 past the largest positive value or subtracted past the smallest negative
 value.</p>

<pre>
    wire final_carry_in;

    CarryIn_Binary
    #(
        .WORD_WIDTH (1)
    )
    calc_final_carry_in
    (
        .A          (accumulated_value  [WORD_WIDTH-1]),
        .B          (increment          [WORD_WIDTH-1]),
        .sum        (incremented_value  [WORD_WIDTH-1]),
        .carryin    (final_carry_in)
    );

    reg signed_overflow_internal = 1'b0;

    always @(*) begin
        signed_overflow_internal = (carry_out != final_carry_in);
    end
</pre>

<p>Update the accumulator if load or increment is valid. 
 <em>Load overrides increment.</em> 
 Clear the overflow if loading.</p>
<p>If <code>SATURATING</code> is non-zero, load the correct saturated limit value
 (2<sup>WORD_WIDTH</sup> when incrementing or -2<sup>WORD_WIDTH</sup>-1 when
 decrementing) if there was an overflow/underflow.</p>

<pre>
    reg [WORD_WIDTH-1:0]    next_value              = WORD_ZERO;
    reg                     enable_accumulator      = 1'b0;
    reg                     enable_overflow         = 1'b0;
    reg                     clear_overflow          = 1'b0;

    generate
        if (SATURATING == 0) begin: saturate_off
            always @(*) begin
                next_value          = (load_valid == 1'b1) ? load_value : incremented_value;
                enable_accumulator  = (increment_valid == 1'b1) || (load_valid == 1'b1);
                enable_overflow     = (increment_valid == 1'b1);
                clear_overflow      = (load_valid      == 1'b1);
            end
        end
        else begin: saturate_on
            reg increment_sign = 1'b0;
            always @(*) begin
                increment_sign      = increment [WORD_WIDTH-1];
                // Overflow (Adding positive number)
                next_value          = (signed_overflow == 1'b1) && (increment_sign == 1'b0) ? WORD_MAX : incremented_value;
                // Underflow (Adding negative number)
                next_value          = (signed_overflow == 1'b1) && (increment_sign == 1'b1) ? WORD_MIN : next_value;
                next_value          = (load_valid == 1'b1) ? load_value : next_value;
                enable_accumulator  = (increment_valid == 1'b1) || (load_valid == 1'b1);
                enable_overflow     = (increment_valid == 1'b1);
                clear_overflow      = (load_valid      == 1'b1);
            end
        end
    endgenerate
</pre>

<p>Finally, the accumulator and signed_overflow registers.</p>

<pre>
    Register
    #(
        .WORD_WIDTH     (WORD_WIDTH),
        .RESET_VALUE    (INITIAL_VALUE)
    )
    accumulator
    (
        .clock          (clock),
        .clock_enable   (enable_accumulator),
        .clear          (clear),
        .data_in        (next_value),
        .data_out       (accumulated_value)
    );

    Register
    #(
        .WORD_WIDTH     (1),
        .RESET_VALUE    (1'b0)
    )
    overflow
    (
        .clock          (clock),
        .clock_enable   (enable_overflow),
        .clear          (clear_overflow),
        .data_in        (signed_overflow_internal),
        .data_out       (signed_overflow)
    );

endmodule
</pre>

<hr>
<p><a href="./index.html">back to FPGA Design Elements</a>
<center><a href="http://fpgacpu.ca/">fpgacpu.ca</a></center>
</body>
</html>

