<html>
<head>
<link rel="shortcut icon" href="./favicon.ico">
<link rel="stylesheet" type="text/css" href="./style.css">
<meta name="viewport" content="width=device-width, initial-scale=1">
<title>Demultiplexer Binary</title>
</head>
<body>

<p><a href="./Demultiplexer_Binary.v">Source</a></p>

<h1>Binary Demultiplexer</h1>
<p>Connects the <code>word_in</code> input port to one of the words in the <code>words_out</code>
 output port, as selected by the <code>output_port_selector</code> binary address, and
 raises the corresponding valid bit in the <code>valids_out</code> output port.  If the
 <code>output_port_selector</code> value is greater than the number of output words
 specified by the <code>OUTPUT_COUNT</code> parameter, then none of the <code>valids_out</code>
 bits will be set.</p>
<h2>Implementation Options</h2>
<p>Set the <code>BROADCAST</code> parameter to 1 to simply replicate and connect
 <code>word_in</code> to each word in <code>words_out</code>, without any logic. The valid bit
 will indicate which downstream logic should accept the data, and other
 logic can snoop the data if that's part of your larger design.</p>
<p>Set the <code>BROADCAST</code> parameter to 0 to send <code>word_in</code> to <em>only</em> the selected
 word in <code>words_out</code>, whose valid bit is set. All other output words are
 annulled to zero. If no valid bit is set, all output words stay at zero.</p>
<p>Not broadcasting the input to the output costs some logic, but less than it
 appears from a standalone synthesis of the demultiplexer: the inferred
 <a href="./Annuller.html">Annullers</a> will very likely disappear into downstream LUT
 logic, it also makes tracing a simulation easier, and adds some design
 security and robustness since unselected downstream logic cannot snoop or
 accidentally receive other data.</p>
<p>Setting <code>BROADCAST</code> to any value other than 1 or 0 will disconnect
 <code>word_in</code> from <code>words_out</code>, raise some critical warnings in your CAD tool,
 and generally cause a lot of downstream logic to optimize away, so you
 should notice...</p>

<pre>
module Demultiplexer_Binary
#(
    parameter       BROADCAST           = 0,
    parameter       WORD_WIDTH          = 0,
    parameter       ADDR_WIDTH          = 0,
    parameter       OUTPUT_COUNT        = 0,

    // Do not set at instantiation
    parameter   TOTAL_WIDTH = WORD_WIDTH * OUTPUT_COUNT
)
(
    input   wire    [ADDR_WIDTH-1:0]    output_port_selector,
    input   wire    [WORD_WIDTH-1:0]    word_in,
    output  wire    [TOTAL_WIDTH-1:0]   words_out,
    output  wire    [OUTPUT_COUNT-1:0]  valids_out
);
</pre>

<p>Convert the binary <code>output_port_selector</code> to a single one-hot bit vector
 which signals which output port will receive the input word.</p>

<pre>
    Binary_to_One_Hot
    #(
        .BINARY_WIDTH   (ADDR_WIDTH),
        .OUTPUT_WIDTH   (OUTPUT_COUNT)
    )
    valid_out
    (
        .binary_in      (output_port_selector),
        .one_hot_out    (valids_out)
    );
</pre>

<p>If we are <em>not</em> broadcasting, then for each output port, annul the output
 if its valid bit is not set.  Thus, only the selected output port will have
 the <code>word_in</code> data. All others will stay at zero.  Otherwise, simply
 replicate and connect the input to all outputs. <a href="./verilog.html#assign">This is one of the very
 rare cases where you must use the <code>assign</code>
 statement.</a></p>

<pre>
    generate
        if (BROADCAST == 0) begin
            genvar i;
            for (i=0; i < OUTPUT_COUNT; i=i+1) begin: per_output
                Annuller
                #(
                    .WORD_WIDTH     (WORD_WIDTH),
                    .IMPLEMENTATION ("AND")
                )
                output_gate
                (
                    .annul          ((valids_out[i] == 1'b0)),
                    .data_in        (word_in),
                    .data_out       (words_out[WORD_WIDTH*i +: WORD_WIDTH])
                );
            end
        end
        else
        if (BROADCAST == 1) begin
            assign words_out = {OUTPUT_COUNT{word_in}};
        end
    endgenerate

endmodule
</pre>

<hr>
<p><a href="./index.html">back to FPGA Design Elements</a>
<center><a href="http://fpgacpu.ca/">fpgacpu.ca</a></center>
</body>
</html>

